apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "net.nonylene.photolinkviewer"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 48
        versionName "2.3.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def FLICKR_KEY
        def TWITTER_KEY
        def TWITTER_SECRET
        def INSTAGRAM_KEY
        def INSTAGRAM_SECRET
        def INSTAGRAM_CALLBACK_SCHEME
        def TUMBLR_KEY

        def file = project.file('key.properties')
        if (file.exists()) {
            def properties = new Properties()
            properties.load(file.newDataInputStream())
            FLICKR_KEY = properties.get("FLICKR_KEY")
            TWITTER_KEY = properties.get("TWITTER_KEY")
            TWITTER_SECRET = properties.get("TWITTER_SECRET")
            INSTAGRAM_KEY = properties.get("INSTAGRAM_KEY")
            INSTAGRAM_SECRET = properties.get("INSTAGRAM_SECRET")
            INSTAGRAM_CALLBACK_SCHEME = properties.get("INSTAGRAM_CALLBACK_SCHEME")
            TUMBLR_KEY = properties.get("TUMBLR_KEY")
        } else {
            FLICKR_KEY = System.getenv("FLICKR_KEY")
            TWITTER_KEY = System.getenv("TWITTER_KEY")
            TWITTER_SECRET = System.getenv("TWITTER_SECRET")
            INSTAGRAM_KEY = System.getenv("INSTAGRAM_KEY")
            INSTAGRAM_SECRET = System.getenv("INSTAGRAM_SECRET")
            INSTAGRAM_CALLBACK_SCHEME = System.getenv("INSTAGRAM_CALLBACK_SCHEME")
            TUMBLR_KEY = System.getenv("TUMBLR_KEY")
        }

        buildConfigField "String", "FLICKR_KEY", "\"${FLICKR_KEY}\""
        buildConfigField "String", "TWITTER_KEY", "\"${TWITTER_KEY}\""
        buildConfigField "String", "TWITTER_SECRET", "\"${TWITTER_SECRET}\""
        buildConfigField "String", "INSTAGRAM_KEY", "\"${INSTAGRAM_KEY}\""
        buildConfigField "String", "INSTAGRAM_SECRET", "\"${INSTAGRAM_SECRET}\""
        buildConfigField "String", "INSTAGRAM_CALLBACK_SCHEME", "\"${INSTAGRAM_CALLBACK_SCHEME}\""
        buildConfigField "String", "TUMBLR_KEY", "\"${TUMBLR_KEY}\""

        manifestPlaceholders = [
                instagramCallbackScheme: INSTAGRAM_CALLBACK_SCHEME
        ]
    }

    buildTypes {
        release {
            buildConfigField "boolean", "IS_CRASHLYTICS_ENABLED", "true"
        }
        debug {
            buildConfigField "boolean", "IS_CRASHLYTICS_ENABLED", "false"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError false
    }
    dexOptions {
        incremental true
    }
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:23.1.1"
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'org.twitter4j:twitter4j-core:4.0.4'
    compile 'org.twitter4j:twitter4j-async:4.0.4'
    compile 'org.twitter4j:twitter4j-http2-support:4.0.4'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile 'com.squareup.okhttp:okhttp:2.7.4'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile ("net.nonylene:photolinkviewer-core:0.0.3-SNAPSHOT") {
        changing = true;
    }

    androidTestCompile('com.android.support.test:runner:0.4'){
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4'){
        exclude module: 'support-annotations'
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true
    }
    compile('com.twitter.sdk.android:twitter:1.11.0@aar') {
        transitive = true
    }
}

afterEvaluate {
    def propertiesFile = file('fabric.properties')
    if (!propertiesFile.exists()) {
        ant.propertyfile(file: "fabric.properties") {
            entry(key: "apiKey", value: System.getenv("FABRIC_KEY"))
            entry(key: "apiSecret", value: System.getenv("FABRIC_SECRET"))
        }
    }
}

buildscript {
    ext.kotlin_version = '1.0.0'
    repositories {
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        url 'https://maven.fabric.io/public'
    }
    maven {
        url "https://nonylene.github.io/PhotoLinkViewer-Core/repository/snapshots/"
    }
}
